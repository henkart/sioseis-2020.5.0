      SUBROUTINE MUTE(BUF,IS,IE,SR,NSAMPS)
!     MUTE APPLIES A MUTE (FRONT END OR SURGICAL) TO A TRACE.  A 20 MIL LINEAR
!  TAPER IS APPLIED IN THE MUTE ZONE TO MERGE THE LIVE DATA TO DEAD DATA
!  WITHOUT A LARGE DISCONTINUITY.
!
!  ARGUMENTS:
!   BUF   - THE ARRAY CONTAINING THE FLOATING OINT TRACE T6 BE MUTED.
!   IS     - THE INDEX IN BUF TO START THE MUTE.
!   IE     - THE INDEX IN BUF TO END THE MUTE.
!   SR     - THE SAMPLE INTERVAL IN SECONDS.
!   NSAMPS - THE NUMBER OF SAMPLES IN THE TRACE.
!
!    PAUL HENKART, SCRIPPS INSTITUTION OF OCEANOGRAPHY, JULY 1979
!   mod 31 Jan 1990 pch to not taper if cloase to the ends of the data
!
      PARAMETER (N=5)
      DIMENSION BUF(1),TAPER(N)
      DATA TAPER/.8,.6,.4,.2,0./
      ISTART=IS
      IEND=IE
!****
!****    APPLY THE FRONT TAPER ONLY IF IT IS A SURGICAL MUTE
!****
      IF( ISTART .NE. 1 ) THEN
          DO 150 I = 1, N
             J = ISTART + I - 1
             BUF(J) = BUF(J) * TAPER(I)
  150     CONTINUE
          ISTART = ISTART + N
      ENDIF
!****
!****   APPLY THE REAR TAPER IF IT DOESN'T GO TO THE END OF THE TRACE
!****
      IF( IE .LE. NSAMPS-n ) THEN
          DO 250 I=1,N
              J=IEND-I+1
             BUF(J)=BUF(J)*TAPER(I)
  250     CONTINUE
          IEND = IEND - N
      ENDIF
!****
!****    NOW ZERO OUT THE DATA BETWEEN ISTART AND IEND
!****
      DO I = ISTART, IEND
  400    BUF(I) = 0.
      ENDDO
      RETURN
      END
