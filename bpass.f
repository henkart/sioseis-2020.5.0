      SUBROUTINE BPASS(FL,FH,FILT,NPTS,SI,SCR)
!    BPASS1 GENERATES A ZERO PHASE TIME DOMAIN FILTER.  THE IMPULSE RESPONSE
! WILL HAVE AN ENERGY OF 1., THUS ALLEVIATING SCALING PROBLEMS DUE TO DIFFERENT
! BANDWIDTHS.
!    THE DESIGN IS DONE IN THE TIME DOMAIN AND IS THE SHORTEST AND SIMPLIEST,
! BUT IS NOT AS GOOD (STEEPNESS OF CUT OFF) AS SOME OPTIMIZING METHODS.
! THE FILTER IS BETTER THE MORE POINTS IT HAS, AND IS WORSE THE LESS POINTS IT
! HAS.  39 POINTS IS A NICE NUMBER
!    THE METHOD OF DESIGN IS:
!   1) GENERATE A COSINE AT A FREQUENCY W1 WHICH IS HALFWAY BETWEEN THE CUTOFF
!     IN THE FREQUENCY DOMAIN THIS IS A SPIKE AT FREQUENCY W1.
!   2) MULTIPLY 1 BY A SINC FUNCTION OF FREQUENCY WC WHICH IS HALF THE BANDWIDTH
!     IN THE FREQUENCY DOMAIN THIS IS CONVOLVING 1 WHITH A BOX CAR.
!   3) MULTIPLY THE RESULT BY A WINDOW TO PREVENT DISCONTINUITIES.  IN THE
!     FREQUENCY DOMAIN THIS IS CONVOLVING THE BOX CAR WITH A SMOOTHING FUNCTION.
!
!  ARGUMENTS:
!      FL     - THE FREQUENCY OF THE LOW CORNER
!      FH     - THE FREQUENCY OF THE HIGH CORNER
!      FILT   - THE ARRAY TO RECEIVE THE TIME DOMAIN FILTER POINTS.
!      NPTS   - THE NUMBER OF FILTER POINTS WANTED
!      SI     - THE SAMPLE INTERVAL IN SECONDS
!      SCR    - A SCRATCH ARRAY AT LEAST NPTS/2+1 LONG
!
!
!  WRITTEN AND COPYRIGHTED BY:
!  PAUL HENKART, SCRIPPS INSTITUTION OF OCEANOGRAPHY, FEBRUARY 1979
!  ALL RIGHTS ARE RESERVED BY THE AUTHOR.  PERMISSION TO COPY OR REPRODUCE THIS
!  SUBROUTINE, BY COMPUTER OR OTHER MEANS, MAY BE OBTAINED ONLY FROM THE AUTHOR.
!  mod 6 Oct 09 - change the documentation - sample interval, not sample rate.
!
      DIMENSION FILT(*),SCR(*)
      PI2=6.283197
      N=NPTS/2+1                                                        ! THE CENTRE POINT OF A ZERO PHASE FILTER
      W1=(FL+FH)/2.                                                     ! FIND THE CENTRE FREQUENCY
      WC=(FH-FL)/2.                                                     ! THE WIDTH OF THE BANDPASS
!****
!****    GENERATE A COSINE WAVE
!****
      P=1./W1/SI
      DO I=1,N
         SCR(I)=COS(FLOAT(I-1)/P*PI2)
      ENDDO
!****
!****   MULTIPLY BY A HALF SINC
!****
      P=1./WC/SI
      SCR(1)=1.
      DO I=2,N
         X=FLOAT(I-1)/P*PI2
         SCR(I)=SCR(I)*SIN(X)/X
      ENDDO
!****
!****     MULTIPLY BY A GOOD WINDOW  ( HAMMING)
!****
      CALL WINDOW(-6,FILT,N,DUM)
      DO I=1,N
         SCR(I)=SCR(I)*FILT(N-I+1)
      ENDDO
!****
!****      NOW MAKE A SYMMETRICAL FILTER OUT OF IT
!****
      DO 400 I=1,N
      FILT(I)=SCR(N-I+1)
      FILT(N+I-1)=SCR(I)
  400 CONTINUE
!****
!****        MAKE THE FILTER HAVE AN ENERGY OF 1.
!****
      E=0.
      DO I=1,NPTS
         E=E+FILT(I)*FILT(I)
      ENDDO
      C=1./SQRT(E)
      DO I=1,NPTS
         FILT(I)=FILT(I)*C
      ENDDO
      RETURN
      END
