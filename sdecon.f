      SUBROUTINE SDECON(I1,I2,I3,I4,I5,I6,I7,I8,I9,I10,I11,I12)
!     PDECON PERFORMS SPIKING DECONVOLUTION IN THE AP.
!  PDECON DESIGNS A LEAST SQUARES PREDICTION ERROR FILTER
!  AND APPLIES THE FILTER TO THE ENTIRE TRACE.
!
!  THE COMPUTATIONAL STEPS ARE:
!  1)  COMPUTE THE POSITIVE LAGS OF THE AUTOCORRELATION OF THE DESIGN WINDOW.
!  2)  COMPUTE AND ADD TO THE ZERO LAG THE PERCENTAGE PREWHITENING.
!  3)  COMPUTE THE CROSSCORRELATION BETWEEN THE DESIGN WINDOW AND THE DESIRED
!      OUTPUT.
!  4)  SOLVE THE NORMAL EQUATIONS USING THE WIENER-LEVINSON ALGORITHIM.
!  5)  NEGATE THE FILTER COEFS. AND MOVE THE FILTER DOWN THE ARRAY THE
!      PREDICTION DISTANCE.
!  6)  PUT A ONE IN THE FIRST FILTER POINT
!  7)  ZERO THE PREDICTION DISTANCE -1 POINTS AFTER THE FIRST POINT.
!  8)  FILTER THE ENTIRE TRACE.
!
!  ARGUMENTS:
!  I1 = DESIGN - THE AP LOCATION OF THE BEGINNING OF THE DESIGN WINDOW.
!  I2 = NDPTS  - THE NUMBER OF DATA POINTS IN THE DESIGN WINDOW
!  I3 = TRACE  - THE AP LOCATION OF THE BEGINNING OF THE APPLICATION WINDOW.
!  I4 = NPTS   - THE NUMBER OF DATA POINTS TO FILTER (THE LENGTH OF THE
!                APPLICATION WINDOW).
!  I5 = WHITEN - THE AP LOCATION OF A 3 WORD ARRAY CONTAINING:
!            1) THE FRACTION OF THE ZERO LAG TO USE AS PREWHITING (PERCENT/100.)
!            2) THE NUMBER ONE (1)
!            3) A SCRATCH LOCATION FOR PDECON TO PUT A -1 INTO!
!  I6 = NFPTS  - THE NUMBER OF FILTER POINTS IN THE FILTER.  THE AUTOCORRELATION
!                AND THE CROSSCORRELATION AND THE FILTER WILL BE NFPTS LONG.
!  I7 = AUTO   - THE AP LOCATION OF WHERE PDECON CAN PUT THE AUTOCORRELATION AND
!                THE CROSSCORELATION.  MUST BE 2*NFPTS LONG.
!  I8 = PREDIC - THE PREDTICTION DISTANCE.
!  I9 = FILT   - THE AP LOCATION OF WHERE PDECON CAN PUT THE FILTER POINTS.
!                (MUST BE NFPTS+PREDICTION DISTANCE LONG).
!  I10= ERROPR - THE AP LOCATION OF WHERE PDECON CAN PUT THE ERROR OPERATOR.
!                (MUST BE NPTS LONG).
!  I11= RESULT - THE AP LOCATION OF WHERE PDECON CAN LEAVE THE RESULTS.
!                (MUST BE NPTS LONG).
!  I12= SCRATC - THE AP LOCATION OF A SCRATCH ARRAY AT LEAST NPTS+NFPTS LONG.
!
!  COPYRIGHTED BY:
!  PAUL HENKART, SCRIPPS INSTITUTION OF OCEANOGRAPHY, JUNE 1980
!
      DESIGN=I1
      NDPTS=I2
      TRACE=I3
      NPTS=I4
      WHITEN=I5
      ONE=WHITEN+1                                                      ! THE AP LOCATION OF THE NUMBER 1.
      NFPTS=I6
      AUTO=I7
      PREDIC=I8
      FILT=I9
      ERROPR=I10
      RESULT=I11
      SCRATC=I12
!
      N=NFPTS+PREDIC
      CROSS=AUTO+N
      CALL CONV(DESIGN,1,DESIGN,1,AUTO,1,NFPTS,NDPTS)                   ! COMPUTE THE AUTOCOR
      CALL VDIV(AUTO,1,ONE,1,SCRATC,1,1)                                ! MAKE THE ZERO LAG = 1.0
      CALL VSMUL(AUTO,1,SCRATC,AUTO,1,NFPTS)
      CALL VSMA(AUTO,1,WHITEN,AUTO,1,AUTO,1,1)                          ! ADD THE PREWHITENER IN
      CALL VMOV(ONE,1,CROSS,1,1)                                        ! MAKE THE CROSSCORRELATION = 1,0,0,0,0
      ITEMP=CROSS+1
      CALL VCLR(ITEMP,1,NFPTS)                                          ! DOES WIENER NEED THIS WHEN SPIKING?
      CALL WIENER(NFPTS,AUTO,CROSS,FILT,ERROPR,1)                       ! COMPUTE THE  FILTER
      ITEMP=TRACE+NPTS
      ITEMP=ITEMP-1
      ITEMP2=N-1
      ITEMP1=SCRATC+NPTS
      ITEMP1=ITEMP1+ITEMP2
      ITEMP1=ITEMP1-1
      CALL VMOV(ITEMP,-1,ITEMP1,-1,NPTS)                                ! MOVE THE TRACE DOWN A FILTER LENGTH
      CALL VCLR(SCRATC,1,ITEMP2)                                        ! PAD THE FRONT OF THE TRACE WITH ZEROES
      ITEMP=FILT+NFPTS
      ITEMP=ITEMP-1
      CALL CONV(SCRATC,1,ITEMP,-1,RESULT,1,NPTS,N)                      ! DO THE CONVOLUTION
      RETURN
      END
