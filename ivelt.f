      SUBROUTINE IVELT(VTP,AVEVEL,ST,TSI,NSAMPS)
!      IVELT IS A SUBPROGRAM TO CONVERT VELOCITY TIME PAIRS INTO AN ARRAY OF
!  VELOCITIES, EACH SEPARATED IN TIME UNIFORMLY.  (I.E.  THERE'S A VELOCITY
!  FOR EACH TIME SAMPLE.)  LINEAR INTERPOLATION IS USED TO OBTAIN VELOCITIES
!  BETWEEN THE TIMES SPECIFIED.  THE VELOCITY IS HELD CONSTANT FOR TIMES OUTSIDE
!  THE FIRST AND LAST SPECIFIED TIMES. AT LEAST 2 PAIRS MUST BE GIVEN.
!  EXCEED THE BOUNDS OF THE VTP.
!       ARGUMENTS:
!            VTP    - AN ARRAY OF VELOCITY-TIME PAIRS.  VTP(1) IS A VELOCITY,
!                     VTP(2) IS THE TIME, IN SECONDS, OF THE VELOCITY VTP(1),
!                     VTP(3) IS THE NEXT VELOCITY, AND VTP(4) IS THE TIME OF
!                     VTP(3), ETC..  ALL TIMES MUST BE STRICTLY MONOTONICALLY
!                     INCREASING.  IF ST<VTP(2) THEN ALL TIMES FROM ST TO VTP(2)
!                     WILL BE SET TO VTP(1)  ( EXTRAPOLATED.).  LIKEWISE, ALL
!                     TIMES EXCEEDING THE LAST VTP TIME WILL BE SET TO THE LAST
!                     VELOCITY.  THE VTP ARRAY MUST BE TERMINATED BY A TIME <=0
!                     OR A TIME EXCEEDING THE LAST VTP.
!            AVEVEL - AN ARRAY TO RECEIVE THE UNIFORMLY SPACED VELOCITIES.  MUST
!                     BE AT LEAST NSAMPS LONG.
!            ST     - START TIME IN SECONDS OF THE AVEVEL ARRAY.  AVEVEL(1)
!                     WILL HAVE A TIME OF ST ASSOCIATED WITH IT.
!            TSI    - TIME SAMPLE INTERVAL, IN SECONDS.  THE TIME DIFFERENCE
!                     BETWEEN CONSEQUTIVE TIME SAMPLES.
!            NSAMPS - THE NUMBER OF TIME SAMPLES.  THE NUMBER OF VELOCITIES TO
!                     COMPUTE AND STORE INTO AVEVEL.
!
!      PAUL HENKART, SCRIPPS INSTITUTION OF OCEANOGRAPHY, MAY 1979
!
      DIMENSION VTP(111),AVEVEL(111)
      TIME=ST
      INDX=1
      N2=1                                                               ! THE POINTER TO A VTP PAIR
!
!    EXTRAPOLATE ANY TIMES LESS THAN VTP(2)
!
   10 IF(TIME.GT.VTP(2)) GO TO 100
!
      AVEVEL(INDX)=VTP(1)
      INDX=INDX+1
      TIME=TIME+TSI
      IF(INDX.GT.NSAMPS) RETURN
      GO TO 10
  100 IF(ST.LE.VTP(N2+3)) GO TO 200
      N2=N2+2                                                            ! STILL TRYING TO FIND A VTP
      IF(VTP(N2+1).GE.0) GO TO 100
      PRINT 120,ST
  120 FORMAT(' ***  ERROR  ***  NO TIME IN THE VTP EXCEEDS THE START ',
     *     'TIME OF ',F10.2,' (IVELT)')
      STOP
!
!    INTERPOLATE VALUES BETWEEN VTP TIMES
!
  200 CONTINUE
      IF(VTP(N2+3).NE.VTP(N2+1)) GO TO 205                              ! CONSTANT VELOCITY
      VT=0.                                                             ! WATCH OUT FOR DIVIDE BY 0.!
      GO TO 210
  205 VT=(VTP(N2+2)-VTP(N2))/(VTP(N2+3)-VTP(N2+1))
  210 CONTINUE
      IF(TIME.LE.VTP(N2+3)) GO TO 300
      N2=N2+2                                                           !  SET UP THE NEXT VTP PAIR
      IF(VTP(N2+3).GT.0) GO TO 200
      V=AVEVEL(INDX-1)                                                  !  EXTRAPOLATE TO TO END
      DO I=INDX,NSAMPS
  220    AVEVEL(I)=V
      ENDDO
      RETURN
  300 AVEVEL(INDX)=(TIME-VTP(N2+1))*VT+VTP(N2)
      INDX=INDX+1
      TIME=TIME+TSI
      IF(INDX.LE.NSAMPS) GO TO 210
      RETURN
      END
